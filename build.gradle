buildscript {
	repositories {
		maven {
			url = "https://maven.fabricmc.net"
		}
		maven {
			url = "https://maven.minecraftforge.net"
		}
		maven {
			url = "https://maven.parchmentmc.org"
		}
		maven {
			url = "https://repo.u-team.info"
		}
		mavenCentral()
	}
	dependencies {
		classpath group: "fabric-loom", name: "fabric-loom.gradle.plugin", version: "0.12.21", changing: true
		classpath group: "info.u-team.gradle_files", name: "gradle_files", version: "1.1.1"
		classpath group: "info.u-team.curse_gradle_uploader", name: "curse_gradle_uploader", version: "1.5.0"
	}
}

apply plugin: "fabric-loom"
apply plugin: "info.u_team.gradle_files"
apply plugin: "info.u_team.curse_gradle_uploader"
apply plugin: "maven-publish"

group = "info.u-team"
archivesBaseName = gradlefiles.archivesBaseName()
version = gradlefiles.version()

gradlefiles {
	vendor "Team UTeam"
	
	signJars true
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withDeobfJar()
	withSourcesJar()
	withJavadocJar()
}

loom {
	accessWidenerPath = file("src/main/resources/musicplayer.accesswidener")
	
	runs {
		client {
			client()
			
			property("musicplayer.dev", "${project.rootDir}")
		}
	}
}

tasks.withType(Jar) { task ->
	gradlefiles.defaultJar(task)
}

tasks.withType(Javadoc) { task ->
	task.options.addStringOption("Xdoclint:none", "-quiet")
}

sourceSets {
	main {
		java {
			srcDirs += "src/data/java"
		}
		resources {
			srcDirs += "src/generated/resources"
		}
	}
}

repositories {
}

dependencies {
	minecraft "com.mojang:minecraft:${config.minecraft.version}"
	mappings loom.officialMojangMappings()
	
	modImplementation "net.fabricmc:fabric-loader:${config.fabric.loader.version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${config.fabric.api.version}"
	
	modImplementation "info.u-team:u_team_core-fabric-1.18.2:4.0.0.1"
	
	implementation project(":musicplayer-lavaplayer-api")
	implementation ("net.harawata:appdirs:1.0.0") {
		transitive=false
	}
}

processResources {
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

curseforge {
	apiKey = defaultPropertyValue("curseforge.token")
	
	project {
		id = config.curse.id
		releaseType = config.curse.type
		
		addGameVersion config.forge.mcversion
		addGameVersion "Fabric"
		addGameVersion "Java 17"
		
		mainArtifact(tasks.getByName("jar")) {
			displayName = "${config.curse.display}-${config.minecraft.version}-${project.version}"
			changelogType = "html"
			changelog = "<a href=\"${gradlefiles.changelogUrl()}\">Changelog of ${project.version}</a>"
		}
	}
}

publishing {
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = defaultPropertyValue("maven.password")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifactId archivesBaseName
			
			from components.java
			
			pom {
				name = project.archivesBaseName
				description = "${config.github.name}"
				url = "https://github.com/MC-U-Team/${config.github.name}"
				scm {
					url = "https://github.com/MC-U-Team/${config.github.name}"
					connection = "scm:git:git://github.com/MC-U-Team/${config.github.name}.git"
					developerConnection = "scm:git:git@github.com:MC-U-Team/${config.github.name}.git"
				}
				issueManagement {
					system = "github"
					url = "https://github.com/MC-U-Team/${config.github.name}/issues"
				}
				licenses {
					license {
						name = "Apache License 2.0"
						url = "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/LICENSE"
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
					developer {
						name = "MrTroble"
					}
				}
			}
		}
	}
}