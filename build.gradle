buildscript {
	repositories {
		maven {
			url = "https://maven.fabricmc.net"
		}
		maven {
			url = "https://maven.minecraftforge.net"
		}
		maven {
			url = "https://maven.parchmentmc.org"
		}
		maven {
			url = "https://repo.u-team.info"
		}
		maven {
			url "https://plugins.gradle.org/m2"
		}
		mavenCentral()
	}
	dependencies {
		classpath group: "fabric-loom", name: "fabric-loom.gradle.plugin", version: "0.12-SNAPSHOT", changing: true
		classpath group: "info.u-team.gradle_files", name: "gradle_files", version: "1.1.1"
		classpath group: "info.u-team.curse_gradle_uploader", name: "curse_gradle_uploader", version: "1.5.0"
		classpath group: "gradle.plugin.com.github.johnrengelman", name: "shadow", version: "7.1.2"
	}
}

apply plugin: "fabric-loom"
apply plugin: "info.u_team.gradle_files"
apply plugin: "info.u_team.curse_gradle_uploader"
apply plugin: "maven-publish"
apply plugin: "com.github.johnrengelman.shadow"

group = "info.u-team"
archivesBaseName = gradlefiles.archivesBaseName()
version = gradlefiles.version()

gradlefiles {
	vendor "Team UTeam"
	
	signJars true
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withDeobfJar()
	withSourcesJar()
	withJavadocJar()
}

loom {
	accessWidenerPath = file("src/main/resources/musicplayer.accesswidener")
	
	runs {
		client {
			client()
			
			property("musicplayer.dev", "${project.rootDir}")
		}
	}
}

configurations {
	shade
	library
	library.extendsFrom(shade)
	implementation.extendsFrom(library)
}

tasks.shadowJar.enabled(false)
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.create("dependencyJar", ShadowJar) {
	archiveClassifier = "shadow-dev"
	
	configurations = [project.configurations.shade]
	
	dependsOn("jar")
	
	from sourceSets.main.output
	from "musicplayer-lavaplayer/src/main/resources/LICENSE-DEPENDENCIES"
	
	dependsOn ":musicplayer-lavaplayer-api:build"
	dependsOn ":musicplayer-lavaplayer:build"
	
	from "musicplayer-lavaplayer-api/build/classes/java/main"
	
	into ("dependencies") {
		from "musicplayer-lavaplayer/build/libs/musicplayer-lavaplayer.jar.packed"
		from "musicplayer-lavaplayer/build/dependencies"
	}
	
	relocate "net.harawata.appdirs", "info.u_team.music_player.shade.net.harawata.appdirs"
	
	exclude "META-INF/maven/**"
}

tasks.create("dependencyDeobfJar", ShadowJar) {
	archiveClassifier = "deobf"
	
	configurations = [project.configurations.shade]
	
	dependsOn("deobfJar")
	
	from sourceSets.main.output
	from "musicplayer-lavaplayer/src/main/resources/LICENSE-DEPENDENCIES"
	
	dependsOn ":musicplayer-lavaplayer-api:build"
	dependsOn ":musicplayer-lavaplayer:build"
	
	from "musicplayer-lavaplayer-api/build/classes/java/main"
	
	into ("dependencies") {
		from "musicplayer-lavaplayer/build/libs/musicplayer-lavaplayer.jar.packed"
		from "musicplayer-lavaplayer/build/dependencies"
	}
	
	relocate "net.harawata.appdirs", "info.u_team.music_player.shade.net.harawata.appdirs"
	
	exclude "META-INF/maven/**"
}

afterEvaluate {
	tasks.getByName("signDependencyJar") { task ->
		task.onlyIf {
			false
		}
	}
	tasks.getByName("signDependencyDeobfJar") { task ->
		task.onlyIf {
			false
		}
	}
	tasks.getByName("prepareRemapJar") { task ->
		task.dependsOn("dependencyJar")
	}
	tasks.getByName("remapJar") { task ->
		task.dependsOn("dependencyJar")
		task.input.set tasks.getByName("dependencyJar").archiveFile.get()
	}
	tasks.getByName("signDeobfJar") { task ->
		task.dependsOn("dependencyDeobfJar")
	}
}

tasks.withType(Jar) { task ->
	gradlefiles.defaultJar(task)
}

tasks.withType(Javadoc) { task ->
	task.options.addStringOption("Xdoclint:none", "-quiet")
}

sourceSets {
	main {
		java {
			srcDirs += "src/data/java"
		}
		resources {
			srcDirs += "src/generated/resources"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${config.minecraft.version}"
	mappings loom.officialMojangMappings()
	
	modImplementation "net.fabricmc:fabric-loader:${config.fabric.loader.version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${config.fabric.api.version}"
	
	modImplementation "info.u-team:u_team_core-fabric-1.18.2:4.0.0.2"
	
	library project(":musicplayer-lavaplayer-api")
	shade ("net.harawata:appdirs:1.0.0") {
		transitive=false
	}
}

processResources {
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

curseforge {
	apiKey = defaultPropertyValue("curseforge.token")
	
	project {
		id = config.curse.id
		releaseType = config.curse.type
		
		addGameVersion config.minecraft.version
		addGameVersion "Fabric"
		addGameVersion "Java 17"
		
		relations {
			requiredDependency "u-team-core"
			requiredDependency "fabric-api"
		}
		
		mainArtifact(tasks.getByName("remapJar")) {
			displayName = "${config.curse.display}-${config.minecraft.version}-${project.version}"
			changelogType = "html"
			changelog = "<a href=\"${gradlefiles.changelogUrl()}\">Changelog of ${project.version}</a>"
		}
	}
}

publishing {
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = defaultPropertyValue("maven.password")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifactId archivesBaseName
			
			from components.java
			
			pom {
				name = project.archivesBaseName
				description = "${config.github.name}"
				url = "https://github.com/MC-U-Team/${config.github.name}"
				scm {
					url = "https://github.com/MC-U-Team/${config.github.name}"
					connection = "scm:git:git://github.com/MC-U-Team/${config.github.name}.git"
					developerConnection = "scm:git:git@github.com:MC-U-Team/${config.github.name}.git"
				}
				issueManagement {
					system = "github"
					url = "https://github.com/MC-U-Team/${config.github.name}/issues"
				}
				licenses {
					license {
						name = "Apache License 2.0"
						url = "https://github.com/MC-U-Team/${config.github.name}/blob/${config.github.branch}/LICENSE"
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
					developer {
						name = "MrTroble"
					}
				}
			}
		}
	}
}